`timescale 1ns / 1ps
/*****************************************************

// Description	: 		SDRAM状态控制模块
//	实现功能			：P1.上电初始200us稳定期控制
								P2.延时计数器（用于状态机时序控制）
								P3.SDRAM初始化，状态机的实现 init_state_r
								P4.定时产生自刷新请求(每15us)
								P5.工作状态实时跟踪	work_state_r(即：SDRAM的读写以及自刷新操作状态机)
								P6.计数器控制逻辑
*****************************************************/
module sdram_ctrl(
				clk,
				rst_n,
				sdram_wr_req,
				sdram_rd_req,
				sdram_wr_ack,
				sdram_rd_ack,
				init_state,
				work_state,
				cnt_clk
			);
			

input clk;				//系统时钟，100MHz(时钟周期：10ns)
input rst_n;			//复位信号，低电平有效


// SDRAM封装接口	
input sdram_wr_req;			//系统写SDRAM请求信号
input sdram_rd_req;			//系统读SDRAM请求信号

output sdram_wr_ack;		//系统写SDRAM响应信号,作为wrFIFO的输出有效信号
output sdram_rd_ack;		//系统读SDRAM响应信号	
//output sdram_ref_w;		// SDRAM自刷新请求信号

//output sdram_busy;			// SDRAM忙标志位，高表示忙
//output sys_dout_rdy;			// SDRAM数据输出完成标志


// SDRAM内部接口
output[4:0] init_state;		// SDRAM初始化寄存器
output[3:0] work_state;	// SDRAM工作状态寄存器
output[8:0] cnt_clk;			//时钟计数



wire sdram_init_done;		// SDRAM初始化完成标志，高表示完成
wire sdram_busy;				// SDRAM忙标志，高表示SDRAM处于工作中
reg sdram_ref_req;			// SDRAM自刷新请求信号
wire sdram_ref_ack;		// SDRAM自刷新请求应答信号

`include "sdr_para.v"		// 包含SDRAM参数定义模块


// SDRAM时序延时参数
parameter	TRP_CLK		= 9'd4,//TRP=18ns预充电有效周期(预充电命令后到行有效命令之间的延迟时间)，precharge command period
					TRFC_CLK		= 9'd6,//TRC=60ns自动预刷新周期
					TMRD_CLK		= 9'd6,//模式寄存器设置等待时钟周期
					TRCD_CLK		= 9'd2,//从行有效到数据读写的时间间隔（一般可取2时钟周期或者三个时钟周期）
					TCL_CLK			= 9'd2,//潜伏期TCL_CLK=2个CLK，在初始化模式寄存器中可设置
					TREAD_CLK	= 9'd8,//突发读数据周期256CLK
					TWRITE_CLK	= 9'd8,//突发写数据256CLK
					TDAL_CLK		= 9'd3;//写入等待


//------------------------------------------------------------------------------
//P1：上电后200us的等待时间（这里时钟周期为10ns，故需要2万个时钟周期）
//------------------------------------------------------------------------------
wire done_200us;		//上电后200us输入稳定期结束标志位
reg[14:0] cnt_200us; 

always @ (posedge clk or negedge rst_n) 
	if(!rst_n) 
			cnt_200us <= 15'd0;
	else if(cnt_200us < 15'd20_000) 
			cnt_200us <= cnt_200us+1'b1;	

assign done_200us = (cnt_200us == 15'd20_000);	//等待时间到则 done_200us信号为1


//------------------------------------------------------------------------------
//P2：延时计数器（用于状态机时序控制）
//------------------------------------------------------------------------------
reg[8:0] cnt_clk_r;		//时钟计数
reg 		cnt_rst_n;		//时钟计数复位信号	

always @ (posedge clk or negedge rst_n) 
	if(!rst_n) 
		cnt_clk_r <= 9'd0;			//计数寄存器复位
	else if(!cnt_rst_n) 
		cnt_clk_r <= 9'd0;	//计数寄存器清零
	else 
		cnt_clk_r <= cnt_clk_r+1'b1;		//启动计数延时
	
assign cnt_clk = cnt_clk_r;			//计数寄存器引出，内部`define中使用 

//------------------------------------------------------------------------------
//P3：SDRAM的初始化操作状态机(共20个状态)
//------------------------------------------------------------------------------
reg[4:0] init_state_r;	// SDRAM初始化状态

always @ (posedge clk or negedge rst_n)
	if(!rst_n) 
		init_state_r <= `I_NOP;
	else 
		case (init_state_r)
		
				//状态0：上电复位后200us结束则进入下一状态
				`I_NOP: 	
					init_state_r <= done_200us ? `I_PRE:`I_NOP;		
				//状态1：预充电状态
				`I_PRE: 	
					init_state_r <= (TRP_CLK == 0) ? `I_AR1:`I_TRP;	
				//状态2：预充电等待TRP_CLK个（4个）时钟周期，其实只要再等2两个时钟周期，因为此时cnt_clk_r=2
				`I_TRP: 	
					init_state_r <= (cnt_clk_r	== TRP_CLK) ? `I_AR1:`I_TRP;			
				
				//状态3：第1次自刷新（这里重新开始计数了），直接跳到状态4
				`I_AR1: 	
					init_state_r <= (TRFC_CLK == 0) ? `I_AR2:`I_TRF1;	
				//状态4：等待第一次自刷新结束，60ns
				`I_TRF1:	
					init_state_r <= (cnt_clk_r	== TRFC_CLK) ? `I_AR2:`I_TRF1;	
				
				//状态5：第二次自刷新
				`I_AR2: 	
					init_state_r <= (TRFC_CLK == 0) ? `I_AR3:`I_TRF2; 
				//状态6：等待第二次自刷新结束，60ns
				`I_TRF2:	
					init_state_r <= (cnt_clk_r	== TRFC_CLK) ? `I_AR3:`I_TRF2; 		
				
				//状态7：第三次自刷新
				`I_AR3: 	init_state_r <= (TRFC_CLK == 0) ? `I_AR4:`I_TRF3; 
				//状态8：等待第三次自刷新结束
				`I_TRF3:	init_state_r <= (cnt_clk_r	== TRFC_CLK) ? `I_AR4:`I_TRF3;	
				
				//状态9，10：第四次自刷新
				`I_AR4: 	init_state_r <= (TRFC_CLK == 0) ? `I_AR5:`I_TRF4; 
				`I_TRF4:	init_state_r <= (cnt_clk_r	== TRFC_CLK) ? `I_AR5:`I_TRF4; 	
				
				//状态11,12：第五次自刷新
				`I_AR5: 	init_state_r <= (TRFC_CLK == 0) ? `I_AR6:`I_TRF5; 
				`I_TRF5:	init_state_r <= (`end_trfc) ? `I_AR6:`I_TRF5;			
				
				//状态13,14：第六次自刷新
				`I_AR6: 	init_state_r <= (TRFC_CLK == 0) ? `I_AR7:`I_TRF6; 
				`I_TRF6:	init_state_r <= (`end_trfc) ? `I_AR7:`I_TRF6;			
				
				//状态15,16：第七次自刷新
				`I_AR7: 	init_state_r <= (TRFC_CLK == 0) ? `I_AR8:`I_TRF7; 
				`I_TRF7: 	init_state_r <= (`end_trfc) ? `I_AR8:`I_TRF7;		
				
				//状态17,18：第8次自刷新
				`I_AR8: 	init_state_r <= (TRFC_CLK == 0) ? `I_MRS:`I_TRF8;	
				`I_TRF8:	init_state_r <= (cnt_clk_r	== TRFC_CLK) ? `I_MRS:`I_TRF8;			
				
				//状态19：模式寄存器设置
				`I_MRS:		init_state_r <= (TMRD_CLK == 0) ? `I_DONE:`I_TMRD;
				
				//状态20：等待模式寄存器设置完成,TMRD_CLK个时钟周期
				`I_TMRD:	init_state_r <= (cnt_clk_r	== TRFC_CLK) ? `I_DONE:`I_TMRD;		
				
				//状态21：初始化完成
				`I_DONE:	init_state_r <= `I_DONE;		
				default: init_state_r <= `I_NOP;
		endcase


assign init_state = init_state_r;
assign sdram_init_done = (init_state_r == `I_DONE);// SDRAM初始化完成标志




//------------------------------------------------------------------------------
//P4：15us计时，每60ms全部4096行存储区进行一次自刷新( 存储体中电容的数据有效保存期上限是64ms )
//------------------------------------------------------------------------------	 
reg[10:0] cnt_15us;	//计数寄存器

always @ (posedge clk or negedge rst_n)
	if(!rst_n) 
		cnt_15us <= 11'd0;
	else if(cnt_15us < 11'd1499) 
		cnt_15us <= cnt_15us+1'b1;	// 60ms(64ms)/4096=15us循环计数
	else 
		cnt_15us <= 11'd0;	

always @ (posedge clk or negedge rst_n)
	if(!rst_n) 
		sdram_ref_req <= 1'b0;
	else if(cnt_15us == 11'd1498) 
		sdram_ref_req <= 1'b1;	//产生自刷新请求（每15us产生一次请求）-->产生工作状态的切换
	else if(sdram_ref_ack) 
		sdram_ref_req <= 1'b0;		//已响应自刷新 

		
		
//------------------------------------------------------------------------------
//P5：SDRAM的读写以及自刷新操作状态机
//------------------------------------------------------------------------------
reg[3:0] work_state_r;		// SDRAM读写状态
reg 			sys_r_wn;				// SDRAM读/写控制信号，读SDRAM：1，写SDRAM：0

always @ (posedge clk or negedge rst_n) begin
	if(!rst_n) 
		work_state_r <= `W_IDLE;
	else 
		case (work_state_r)
			//状态0：空闲状态，可能有三个动作：（1.自刷新请求，2.读请求，3.写请求）
 			`W_IDLE:	
						//动作1：产生自刷新请求(CS#=0,RAS#=0,CAS#=0,WE#=1)
						if(sdram_ref_req & sdram_init_done) 
							begin
								work_state_r <= `W_AR; 		//定时自刷新请求
								sys_r_wn <= 1'b1;
							end 
						
						//动作2：写SDRAM请求,进入行有效状态(CS#=0,RAS#=0,CAS#=1,WE#=1)
						else if(sdram_wr_req & sdram_init_done)//写请求 
							begin
								work_state_r <= `W_ACTIVE;//写SDRAM
								sys_r_wn <= 1'b0;	
							end		
						//动作3：读SDRAM请求,,进入行有效状态
						else if(sdram_rd_req && sdram_init_done)//读请求
							begin
								work_state_r <= `W_ACTIVE;//读SDRAM
								sys_r_wn <= 1'b1;	
							end
						//什么也没有发生..保持IDLE..
						else 
							begin 
								work_state_r <= `W_IDLE;
								sys_r_wn <= 1'b1;
							end	
						
						
			//状态1：行有效，判断读写
			`W_ACTIVE: 	
						if(TRCD_CLK == 0)//不可能正确。。。
							if(sys_r_wn) 
									work_state_r <= `W_READ;
							else 
									work_state_r <= `W_WRITE;
						else 
							work_state_r <= `W_TRCD;

			//状态2：等待RCD延时，然后转入读/写状态			
			`W_TRCD:	 if(cnt_clk_r	== TRCD_CLK-1)
						 	 if(sys_r_wn) 
									work_state_r <= `W_READ;
						 	 else 
									work_state_r <= `W_WRITE;
						else 
							work_state_r <= `W_TRCD;
							
			//状态3：读状态开启，转入潜伏期
			`W_READ:	work_state_r <= `W_CL;
			
			//状态4：潜伏期（只有读SDRAM的时候才有），潜伏期时间到则转入数据读取状态
			`W_CL:		work_state_r <= (cnt_clk_r== TCL_CLK-1) ? `W_RD:`W_CL;	
			
			//状态5：读取数据(看来是要读8次)
			`W_RD:		work_state_r <= (cnt_clk_r	== TREAD_CLK+2) ? `W_RWAIT:`W_RD;	//后面需要添加一个读完成后的预充电等待状态
			
			//状态6：读取后要进行充电，完成后进入空闲状态
			`W_RWAIT:	work_state_r <= (cnt_clk_r	== TRP_CLK) ? `W_IDLE:`W_RWAIT;
			
			//状态7： 写状态开启,不需要进入潜伏期
			`W_WRITE:	work_state_r <= `W_WD;
			
			//状态8：写入等待。。。。
			`W_WD:		work_state_r <= (cnt_clk_r	== TWRITE_CLK-2) ? `W_TDAL:`W_WD;
			
			//状态9：等待写数据并自刷新结束
			`W_TDAL:		work_state_r <= (`end_tdal) ? `W_IDLE:`W_TDAL;
			
			
			//状态10：开始自刷新			
			`W_AR:		
					work_state_r <= (TRFC_CLK == 0) ? `W_IDLE:`W_TRFC; 
					
			//状态11：自刷新等待
			`W_TRFC:	
					work_state_r <= (cnt_clk_r	== TRFC_CLK) ? `W_IDLE:`W_TRFC;
					
			default: 	
					work_state_r <= `W_IDLE;
					
		endcase
end

// SDRAM工作状态寄存器（模块输出）
assign work_state = work_state_r;	
	
// SDRAM忙标志位（模块输出）
//assign sdram_busy = (sdram_init_done && work_state_r == `W_IDLE) ? 1'b0:1'b1;	

// SDRAM自刷新应答信号（内部连线）
assign sdram_ref_ack = (work_state_r == `W_AR);		

//表示请求读出wrFIFO中的数据，写入sdram
//或组合语句：	1.当前处于行有效等待且处于写状态
//							2.当前处于“准备写数据状态”
//							3.当前处于写状态，且计数周期小于6
assign sdram_wr_ack = ((work_state == `W_TRCD) & ~sys_r_wn) | (work_state == `W_WRITE) 
						| ((work_state == `W_WD) & (cnt_clk_r < 9'd6));		

//读SDRAM响应信号		

//原始值				
//assign sdram_rd_ack = (work_state_r == `W_RD) & (cnt_clk_r > 9'd1) & (cnt_clk_r < 9'd10);		
assign sdram_rd_ack = (work_state_r == `W_RD) & (cnt_clk_r >=9'd0) & (cnt_clk_r < 9'd8);


// SDRAM数据输出完成标志
//assign sys_dout_rdy = (work_state_r == `W_RD && `end_tread);		






//------------------------------------------------------------------------------
//P6：计数器控制逻辑
//------------------------------------------------------------------------------
always @ (init_state_r or work_state_r or cnt_clk_r) 
	begin
		case (init_state_r)
			//状态1：计数器清0
	    	`I_NOP:	
				cnt_rst_n <= 1'b0;
			//状态2：预充电状态，延时计数启动，cnt_rst_n=1
	   	`I_PRE:	cnt_rst_n <= (TRP_CLK != 0);	
			//状态3：这里要执行好几次，直到预充电延时计数结束后，清零计数器
	   	`I_TRP:	cnt_rst_n <= (cnt_clk_r	== TRP_CLK) ? 1'b0:1'b1;	
			
			//只要开始新一次自刷新，就将计数器值变成1
	    	`I_AR1,`I_AR2,`I_AR3,`I_AR4,`I_AR5,`I_AR6,`I_AR7,`I_AR8:
	         		cnt_rst_n <= (TRFC_CLK != 0);			//自刷新延时计数启动
						
			//等待自刷新延时计数结束后，清零计数器			
	    	`I_TRF1,`I_TRF2,`I_TRF3,`I_TRF4,`I_TRF5,`I_TRF6,`I_TRF7,`I_TRF8:
	         		cnt_rst_n <= (`end_trfc) ? 1'b0:1'b1;	
						
			`I_MRS:	cnt_rst_n <= (TMRD_CLK != 0);			//模式寄存器设置延时计数启动
			`I_TMRD:	cnt_rst_n <= (cnt_clk_r	== TMRD_CLK) ? 1'b0:1'b1;	//等待自刷新延时计数结束后，清零计数器
			
		   `I_DONE:
						case (work_state_r)
								//空闲
								`W_IDLE:	cnt_rst_n <= 1'b0;
								
								//行有效
								`W_ACTIVE: 	cnt_rst_n <= (TRCD_CLK == 0) ? 1'b0:1'b1;
								
								//RCD等待
								`W_TRCD:	cnt_rst_n <= (cnt_clk_r	== TRCD_CLK-1) ? 1'b0:1'b1;
								
								//潜伏期
								`W_CL:		cnt_rst_n <= (cnt_clk_r   == TCL_CLK-1) ? 1'b0:1'b1;
								
								//
								`W_RD:		cnt_rst_n <= (cnt_clk_r	== TREAD_CLK+2) ? 1'b0:1'b1;
								
								//
								`W_RWAIT:	cnt_rst_n <= (`end_trwait) ? 1'b0:1'b1;
								
								//
								`W_WD:		cnt_rst_n <= (`end_twrite) ? 1'b0:1'b1;
								
								//
								`W_TDAL:	cnt_rst_n <= (`end_tdal) ? 1'b0:1'b1;
								
								//
								`W_TRFC:	
										cnt_rst_n <= (`end_trfc) ? 1'b0:1'b1;
										
								default: cnt_rst_n <= 1'b0;
		         	endcase
		default: 
				cnt_rst_n <= 1'b0;
		endcase
	end

endmodule
